
dnl -- FreeType v2 --------------------------------------------------------------
FREETYPE_INC=NO
AC_MSG_CHECKING([for FreeType2 freetype/t1tables.h])
FREETYPE_INC_DIRS="/usr/include /usr/include/freetype2 /usr/local/include /usr/local/include/freetype2 /usr/X11R6/include/freetype2 $kde_extra_includes"
AC_FIND_FILE(freetype/t1tables.h,$FREETYPE_INC_DIRS,FREETYPE_INC)
if test "$FREETYPE_INC" = NO; then
    AC_MSG_WARN([FreeType2 headers not found!])
else
    AC_MSG_RESULT([yes, $FREETYPE_INC])
    dnl some versions of freetype2 need <ft2build.h>, but place them
    dnl into /x/y if t1tables.h is in /x/y/freetype2/freetype
    ft_dir=$FREETYPE_INC
    FREETYPE_INC=""
    ft_test=`echo $ft_dir | sed 's,^.*/,,'`
    if test "$ft_test" = freetype2; then
      dnl note the outer [] to make autoconf not remove the inner ones
      ft_addinc=[`echo "$ft_dir" | sed 's,/[^/]*$,,'`]
      test "$ft_addinc" = "$ft_dir" || test "$ft_addinc" = /usr/include \
        || test -z "$ft_addinc" || FREETYPE_INC="-I$ft_addinc"
    fi
    FREETYPE_INC="-I$ft_dir $FREETYPE_INC"

    dnl FreeType 2.0.1 has a ftnames.h, whereas 2.0.2 calls it ftsnames.h
    AC_MSG_CHECKING([for FreeType2 freetype/ftsnames.h])
    AC_FIND_FILE(freetype/ftsnames.h,$FREETYPE_INC_DIRS,FT2_FTSNAMES)
    if test "$FT2_FTSNAMES" = NO; then
        AC_MSG_RESULT([no])
    else
        AC_MSG_RESULT([yes])
        AC_DEFINE_UNQUOTED(HAVE_FT2_FTSNAMES, 1, [Define if your system has ftsnames.h])
    fi

    ac_save_LDFLAGS=$LDFLAGS
    LDFLAGS="$LDFLAGS $X_LDFLAGS"
    AC_CHECK_LIB(freetype,main,FREETYPE_LIB="-lfreetype",FREETYPE_LIB="NO")
    if test "$FREETYPE_LIB" = NO; then
        AC_MSG_WARN([FreeType2 library not found!])
    else
        AC_DEFINE_UNQUOTED(HAVE_FT2, 1, [Define if your system has the freetype2 library])
        AC_SUBST(FREETYPE_LIB)
        AC_SUBST(FREETYPE_INC)
    fi
    LDFLAGS=$ac_save_LDFLAGS
fi

if test "$FREETYPE_INC" = "NO" || test "$FREETYPE_LIB" = "NO"; then
    AC_MSG_WARN([Missing FreeType2 - disabling kfontinst!])
    FONTINST_SUBDIR=""
else
    FONTINST_SUBDIR="kfontinst" 
fi

AC_SUBST(FONTINST_SUBDIR)

dnl -- Xft/XRender --------------------------------------------------------------
AC_MSG_CHECKING([for Xft/XRender X11/Xft/Xft.h])
FOUND_XFT=NO
KDE_CHECK_HEADER(X11/Xft/Xft.h,FOUND_XFT="YES",FOUND_XFT="NO")
if test "$FOUND_XFT" = YES; then
    AC_DEFINE_UNQUOTED(HAVE_XFT, 1, [Define if your system has Xft support])
fi


